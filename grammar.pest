// Whitespace
comment = _{ NEWLINE ~ (" " | "\t")* ~ "--" ~ !"!" ~ (!NEWLINE ~ ANY)*}
annotation_token = _{"--!"}
blank = _{ comment | " " | "\t" | NEWLINE }
ws = _{ blank | annotation_token }
significant_whitespace = { blank | annotation_token }

// Numbers
number = {ASCII_DIGIT+}

// Postgres identifiers
ident = { 
    !"_"+
    ~ (("_" | LETTER) ~ (LETTER | ASCII_DIGIT | "_")*) 
}
extended_bind_parameter = _{":" ~ ident}
pg_bind_parameter = _{"$" ~ number}

// Parameters
param_list = {
	"(" ~ ws* 
    ~ (ident ~ ws* 
        ~ ("," ~ ws* ~ ident ~ ws*)*)? 
    ~ ","? ~ ws* 
    ~ ")"
}

// Nullable column list
nullable_column_list = {
	"?{" ~ ws* 
    ~ ((ident | number) ~ ws* 
        ~ ("," ~ ws* ~ (ident | number) ~ ws*)*)? 
    ~ ","? ~ ws* 
    ~ "}"
}

// Query
annotation = {
    annotation_token ~ ws+
    ~ ident
    ~ (ws* ~ param_list)?
    ~ (ws* ~ nullable_column_list)?
    ~ ((" " | "\t")* ~ NEWLINE | significant_whitespace* ~ NEWLINE)
}

query = {
    annotation
    ~ sql
}


// SQL Strings
string_constant = _{
    "'" 
    ~ ("''" | (!"'" ~ ANY))*
    ~ "'"
}
quoted_identifier = _{
    "\"" 
    ~ (!"\"" ~ ANY)* 
    ~ "\""
}
c_style = _{
    "E"
    ~ "'"
    ~ ( "\\\\" | "''" | "\\'" | (!"'" ~ ANY))*
    ~ "'"
}
dollar_quoted = _{
    "$" ~ PUSH((!"$" ~ ANY)*) ~ "$" 
    ~ (!("$" ~ POP ~ "$") ~ ANY)* 
    ~ ("$" ~ POP ~ "$")
}
sql_string = _{string_constant | quoted_identifier | c_style | dollar_quoted}

// SQL Block
sql = {
	(
	    sql_string
        | "::" 
	    | (extended_bind_parameter | pg_bind_parameter)
	    | (!(NEWLINE ~ blank* ~ annotation_token) ~ ANY)
	)+
}

// Parsers
parser = {SOI ~ (blank* ~ query ~ blank*)* ~ EOI}